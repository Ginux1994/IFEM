IF(NOT IFEM_FORCE_SYSTEM_LIB)
  FIND_PATH(IFEM_PATH
    NAMES IFEM.h
    PATHS ${PROJECT_SOURCE_DIR}/../../src/
          ${PROJECT_SOURCE_DIR}/../../../src/)
ENDIF(NOT IFEM_FORCE_SYSTEM_LIB)

IF(IFEM_PATH)
  # Build is in-tree
  MESSAGE(STATUS "Using in-tree libIFEM")

  SET(IFEM_INCLUDES ${IFEM_PATH}
                    ${IFEM_PATH}/ASM
                    ${IFEM_PATH}/Eig
                    ${IFEM_PATH}/LinAlg
                    ${IFEM_PATH}/SIM
                    ${IFEM_PATH}/Utility)

  FIND_LIBRARY(IFEM_LIBRARIES
    NAMES IFEM
    PATHS ${IFEM_PATH}/../${IFEM_BUILD_TYPE}/lib
          ${IFEM_PATH}/../lib
    NO_DEFAULT_PATH)
  IF(NOT IFEM_LIBRARIES)
    MESSAGE(WARNING "Could not find the in-tree libIFEM library, "
      "we assume it will be built into a build-type dir")
  ENDIF(NOT IFEM_LIBRARIES)
  IF (NOT BUILD_SHARED_LIBS)
    FIND_PACKAGE(IFEMDeps REQUIRED)
    SET(IFEM_LIBRARIES ${IFEM_LIBRARIES} ${IFEM_DEPLIBS})
  ENDIF(NOT BUILD_SHARED_LIBS)

  IF(NOT IFEM_USE_SYSTEM_TINYXML)
    SET(IFEM_INCLUDES ${IFEM_INCLUDES}
                      ${IFEM_PATH}/../3rdparty/tinyxml)
  ENDIF(NOT IFEM_USE_SYSTEM_TINYXML)
ELSE(IFEM_PATH)
  IF(NOT DEFINED FORCE_SYSTEM_IFEM OR NOT "${FORCE_SYSTEM_IFEM}")
    MESSAGE(STATUS "No in-tree libIFEM found, looking for system library")
  ENDIF(NOT DEFINED FORCE_SYSTEM_IFEM OR NOT "${FORCE_SYSTEM_IFEM}")

  FIND_PATH(IFEM_INCLUDES
    NAMES SIMbase.h
    PATHS $ENV{HOME}/include
    PATH_SUFFIXES IFEM)

  FIND_LIBRARY(IFEM_LIBRARIES
    NAMES IFEM
    PATHS $ENV{HOME}/lib)

  # system lib always uses system tixml
  FIND_PACKAGE(TinyXML REQUIRED)
  SET(IFEM_DEPLIBS ${IFEM_DEPLIBS} ${TINYXML_LIBRARIES})
ENDIF(IFEM_PATH)

INCLUDE(FindPackageHandleStandardArgs)
find_package_handle_standard_args(IFEM DEFAULT_MSG
                                  IFEM_LIBRARIES)

SET(IFEM_LIBRARIES ${IFEM_LIBRARIES} ${IFEM_DEPLIBS})
SET(IFEM_INCLUDES ${IFEM_INCLUDES} ${IFEM_DEPINCLUDES})

# Needed as we have templates using these flags
ENABLE_LANGUAGE(CXX)
IF(CMAKE_CXX_COMPILER MATCHES icpc)
   SET(IFEM_CXX_FLAGS "${IFEM_CXX_FLAGS} -DUSE_MKL -mkl=sequential")
  SET(IFEM_BUILD_CXX_FLAGS "${IFEM_BUILD_CXX_FLAGS} -DUSE_MKL -mkl=sequential")
ELSE(CMAKE_CXX_COMPILER MATCHES icpc)
  SET(IFEM_CXX_FLAGS "${IFEM_CXX_FLAGS} -DUSE_CBLAS")
  SET(IFEM_BUILD_CXX_FLAGS "${IFEM_BUILD_CXX_FLAGS} -DUSE_CBLAS")
  FIND_PACKAGE(LAPACK REQUIRED)
  SET(IFEM_LIBRARIES ${IFEM_LIBRARIES} ${LAPACK_LIBRARIES})
ENDIF(CMAKE_CXX_COMPILER MATCHES icpc)

SET(IFEM_CXX_FLAGS "${IFEM_CXX_FLAGS} -Dreal=double")
